ext.versionFile = file("./version/version.properties")

task loadVersion(group: 'versioning') {
    project.version = readVersion()
}

task increasePatchVersion(group: 'versioning') {
    inputs.property('patch', project.version.patch)
    outputs.file versionFile

    doLast {
        project.version.patch++
        ant.propertyfile(file: versionFile) {
            entry(key: 'patch', type: 'int', operation: '=', value: "$project.version.patch")
        }
    }
}

task increaseMinorVersion(group: 'versioning') {
    inputs.property('minor', project.version.minor)
    inputs.property('patch', project.version.patch)
    outputs.file versionFile

    doLast {
        project.version.minor++
        project.version.patch = 0
        ant.propertyfile(file: versionFile) {
            entry(key: 'minor', type: 'int', operation: '=', value: "$project.version.minor")
            entry(key: 'patch', type: 'int', operation: '=', value: "$project.version.patch")
        }
    }
}

task increaseMajorVersion(group: 'versioning') {
    inputs.property('major', project.version.major)
    inputs.property('minor', project.version.minor)
    inputs.property('patch', project.version.patch)
    outputs.file versionFile

    doLast {
        project.version.major++
        project.version.minor = 0
        project.version.patch = 0
        ant.propertyfile(file: versionFile) {
            entry(key: 'major', type: 'int', operation: '=', value: "$project.version.major")
            entry(key: 'minor', type: 'int', operation: '=', value: "$project.version.minor")
            entry(key: 'patch', type: 'int', operation: '=', value: "$project.version.patch")
        }
    }


}

ProjectVersion readVersion() {
    Properties versionProperties = new Properties()
    versionFile.withInputStream {
        stream -> versionProperties.load(stream)
    }
    new ProjectVersion(
            versionProperties.major.toInteger(),
            versionProperties.minor.toInteger(),
            versionProperties.patch.toInteger()
    )
}

class ProjectVersion {
    Integer major
    Integer minor
    Integer patch

    ProjectVersion(Integer major, Integer minor, Integer patch) {
        this.major = major
        this.minor = minor
        this.patch = patch
    }

    @Override
    String toString() {
        "$major.$minor.$patch"
    }
}